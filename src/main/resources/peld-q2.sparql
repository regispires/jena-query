PREFIX pref: <http://localhost:2020/vocab/>
PREFIX owl:<http://www.w3.org/2002/07/owl#>
PREFIX dbpedia-owl: <http://dbpedia.org/ontology/>
select * where{
    {
        {
            service<http://virtuoso.mooo.com/peld-plankton/sparql>{
                select distinct ?id_taxon ?id_reg where{
                    ?s pref:peld_analysis_id_taxon ?id_taxon.
                    ?s pref:peld_analysis_id_collect ?id_an.
                    ?id_an pref:peld_collect_local ?id_reg.
                    ?id_an pref:peld_date 2004
                }
            }
        }
        UNION
        {
            service<http://virtuoso.mooo.com/peld-catfish/sparql>{
                select distinct ?id_taxon ?id_reg where {
                    ?s pref:peld_id_taxon ?id_taxon.
                    ?s pref:peld_collect_local ?id_reg.
                    ?s pref:peld_collect_date 2004 .
                }
            }
        }
        UNION
        {
            service<http://virtuoso.mooo.com/peld-fish/sparql>{
                select distinct ?id_taxon ?id_reg where {
                    ?s pref:peld_fish_analysis_id_taxon ?id_taxon.
                    ?s pref:peld_analysis_id_collect ?id_an .
                    ?id_an pref:peld_fish_local_collect ?id_reg.
                    ?id_an pref:peld_date_collect 2004 .
                }
            }
        }
    }
    service<http://virtuoso.mooo.com/peld-region/sparql>{
        select ?id_reg where{
            ?id_reg pref:peld_region_name_reg 'Paqueta' .
        }
    }
    service<http://virtuoso.mooo.com/peld-taxon/sparql>{
        select ?id_taxon ?name ?synonymous ?predator where{
            ?id_taxon pref:peld_taxon_scientific_name  ?name  .
            optional {
                ?id_taxon pref:peld_taxon_is_synonimous_of ?id_synonymous .
                ?id_synonymous pref:peld_taxon_popular_name  ?synonymous
            }
            optional {
                ?chain       pref:peld_taxon_trophic_chain_id_prey_taxon     ?id_taxon .
                ?chain       pref:peld_taxon_trophic_chain_id_predator_taxon ?id_predator .
                ?id_predator pref:peld_taxon_scientific_name                 ?predator  .
            }
            
        }
        
    }
}